// ========================================================================== //
// Copyright (c) 2022 - 2026 NVIDIA Corporation & Affiliates.                 //
// All rights reserved.                                                       //
//                                                                            //
// This source code and the accompanying materials are made available under   //
// the terms of the Apache License 2.0 which accompanies this distribution.   //
// ========================================================================== //

// RUN: cudaq-opt --unitary-synthesis --canonicalize %s | FileCheck %s

module attributes {quake.mangled_name_map = {__nvqpp__mlirgen__kernel = "__nvqpp__mlirgen__kernel_PyKernelEntryPointRewrite"}} {
  func.func @__nvqpp__mlirgen__kernel() attributes {"cudaq-entrypoint"} {
    %0 = quake.alloca !quake.ref
    quake.h %0 : (!quake.ref) -> ()
    quake.custom_op @__nvqpp__mlirgen__custom_s_generator_1.rodata<adj> %0 : (!quake.ref) -> ()
    quake.custom_op @__nvqpp__mlirgen__custom_s_adj_generator_1.rodata %0 : (!quake.ref) -> ()
    quake.h %0 : (!quake.ref) -> ()
    return
  }
  cc.global constant private @__nvqpp__mlirgen__custom_s_generator_1.rodata (dense<[(1.000000e+00,0.000000e+00), (0.000000e+00,0.000000e+00), (0.000000e+00,0.000000e+00), (0.000000e+00,1.000000e+00)]> : tensor<4xcomplex<f64>>) : !cc.array<complex<f64> x 4>
  cc.global constant private @__nvqpp__mlirgen__custom_s_adj_generator_1.rodata (dense<[(1.000000e+00,0.000000e+00), (0.000000e+00,0.000000e+00), (0.000000e+00,0.000000e+00), (-0.000000e+00,-1.000000e+00)]> : tensor<4xcomplex<f64>>) : !cc.array<complex<f64> x 4>
}

// CHECK-LABEL:   func.func private @__nvqpp__mlirgen__custom_s_generator_1.kernel(
// CHECK-SAME:                                                                     %[[VAL_0:.*]]: !quake.ref) {
// CHECK:           %[[VAL_1:.*]] = arith.constant -1.5707963267948966 : f64
// CHECK:           %[[VAL_2:.*]] = arith.constant 1.5707963267948966 : f64
// CHECK:           %[[VAL_3:.*]] = arith.constant 0.78539816339744839 : f64
// CHECK:           quake.rz (%[[VAL_3]]) %[[VAL_0]] : (f64, !quake.ref) -> ()
// CHECK:           quake.rz (%[[VAL_3]]) %[[VAL_0]] : (f64, !quake.ref) -> ()
// CHECK:           quake.r1 (%[[VAL_2]]) %[[VAL_0]] : (f64, !quake.ref) -> ()
// CHECK:           quake.rz (%[[VAL_1]]) %[[VAL_0]] : (f64, !quake.ref) -> ()
// CHECK:           return
// CHECK:         }

// CHECK-LABEL:   func.func private @__nvqpp__mlirgen__custom_s_adj_generator_1.kernel(
// CHECK-SAME:                                                                         %[[VAL_0:.*]]: !quake.ref) {
// CHECK:           %[[VAL_1:.*]] = arith.constant 1.5707963267948966 : f64
// CHECK:           %[[VAL_2:.*]] = arith.constant -1.5707963267948966 : f64
// CHECK:           %[[VAL_3:.*]] = arith.constant -0.78539816339744839 : f64
// CHECK:           quake.rz (%[[VAL_3]]) %[[VAL_0]] : (f64, !quake.ref) -> ()
// CHECK:           quake.rz (%[[VAL_3]]) %[[VAL_0]] : (f64, !quake.ref) -> ()
// CHECK:           quake.r1 (%[[VAL_2]]) %[[VAL_0]] : (f64, !quake.ref) -> ()
// CHECK:           quake.rz (%[[VAL_1]]) %[[VAL_0]] : (f64, !quake.ref) -> ()
// CHECK:           return
// CHECK:         }

// CHECK-LABEL:   func.func @__nvqpp__mlirgen__kernel() attributes {"cudaq-entrypoint"} {
// CHECK:           %[[VAL_0:.*]] = quake.alloca !quake.ref
// CHECK:           quake.h %[[VAL_0]] : (!quake.ref) -> ()
// CHECK:           quake.apply<adj> @__nvqpp__mlirgen__custom_s_generator_1.kernel %[[VAL_0]] : (!quake.ref) -> ()
// CHECK:           quake.apply @__nvqpp__mlirgen__custom_s_adj_generator_1.kernel %[[VAL_0]] : (!quake.ref) -> ()
// CHECK:           quake.h %[[VAL_0]] : (!quake.ref) -> ()
// CHECK:           return
// CHECK:         }
