// ========================================================================== //
// Copyright (c) 2022 - 2023 NVIDIA Corporation & Affiliates.                 //
// All rights reserved.                                                       //
//                                                                            //
// This source code and the accompanying materials are made available under   //
// the terms of the Apache License 2.0 which accompanies this distribution.   //
// ========================================================================== //

// RUN: cudaq-translate --convert-to=qir-base %s | FileCheck %s

module {
  func.func @__nvqpp__mlirgen__ghz() {
// CHECK-LABEL: __nvqpp__mlirgen__ghz
// CHECK: tail call void @__quantum__qis__h__body(%[[VAL_0:.*]]* null)
// CHECK: tail call void @__quantum__qis__cnot__body(%Qubit* null, %Qubit* nonnull inttoptr (i64 1 to %Qubit*))
// CHECK: tail call void @__quantum__qis__cnot__body(%Qubit* nonnull inttoptr (i64 1 to %Qubit*), %Qubit* nonnull inttoptr (i64 2 to %Qubit*))
// CHECK: tail call void @__quantum__qis__mz__body(%[[VAL_0]]* null, %[[VAL_1:.*]]* null)
// CHECK: tail call void @__quantum__qis__mz__body(%[[VAL_0]]* nonnull inttoptr (i64 1 to %[[VAL_0]]*), %[[VAL_1]]* nonnull inttoptr (i64 1 to %[[VAL_1]]*))
// CHECK: tail call void @__quantum__qis__mz__body(%[[VAL_0]]* nonnull inttoptr (i64 2 to %[[VAL_0]]*), %[[VAL_1]]* nonnull inttoptr (i64 2 to %[[VAL_1]]*))
// CHECK: tail call void @__quantum__rt__result_record_output(%Result* null, i8* nonnull
// CHECK: tail call void @__quantum__rt__result_record_output(%Result* nonnull inttoptr (i64 1 to %Result*), i8* nonnull
// CHECK: tail call void @__quantum__rt__result_record_output(%Result* nonnull inttoptr (i64 2 to %Result*), i8* nonnull
// CHECK: ret void
    %c2_i32 = arith.constant 2 : i32
    %c1_i32 = arith.constant 1 : i32
    %c1 = arith.constant 1 : index
    %c0_i32 = arith.constant 0 : i32
    %c0 = arith.constant 0 : index
    %c3_i32 = arith.constant 3 : i32
    %0 = quake.alloca !quake.veq<3>
    %1 = quake.extract_ref %0[%c0_i32] : (!quake.veq<3>,i32) -> !quake.ref
    quake.h %1 : (!quake.ref) -> ()
    %2 = quake.extract_ref %0[%c0] : (!quake.veq<3>, index) -> !quake.ref
    %3 = quake.extract_ref %0[%c1_i32] : (!quake.veq<3>,i32) -> !quake.ref
    quake.x [%2] %3 : (!quake.ref, !quake.ref) -> ()
    %4 = quake.extract_ref %0[%c1] : (!quake.veq<3>,index) -> !quake.ref
    %5 = quake.extract_ref %0[%c2_i32] : (!quake.veq<3>,i32) -> !quake.ref
    quake.x [%4] %5 : (!quake.ref, !quake.ref) -> ()
    %6 = quake.mz %1 : (!quake.ref) -> i1
    %7 = quake.mz %3 : (!quake.ref) -> i1
    %8 = quake.mz %5 : (!quake.ref) -> i1
    return
  }
  // CHECK: attributes #0 = { "irreversible" }
  // CHECK: attributes #1 = { "entry_point" "output_labeling_schema"="schema_id" "output_names"="{{.*}}" "qir_profiles"="base_profile" "requiredQubits"="3" "requiredResults"="3" }
}
