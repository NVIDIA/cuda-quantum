// ========================================================================== //
// Copyright (c) 2022 - 2023 NVIDIA Corporation & Affiliates.                 //
// All rights reserved.                                                       //
//                                                                            //
// This source code and the accompanying materials are made available under   //
// the terms of the Apache License 2.0 which accompanies this distribution.   //
// ========================================================================== //

// RUN: cudaq-opt --apply-op-specialization %s | FileCheck %s

// Test specialization of a user-defined kernel (@test) for both
// adjoint and control forms.

module {
  func.func @test(%arg : !quake.qref) {
    quake.t (%arg)
    quake.h (%arg)
    %1 = arith.constant 1.0 : f32
    quake.rx |%1 : f32| (%arg)
    quake.x (%arg)
    return
  }
  func.func @do_apply(%arg : !quake.qref, %brg : !quake.qref) {
    quake.apply <adj> @test [%brg : !quake.qref] %arg : (!quake.qref) -> ()
    return
  }
}

// CHECK-LABEL: func.func private @test.adj.ctrl(
// CHECK-SAME:     %[[VAL_0:.*]]: !quake.qvec<?>, %[[VAL_1:.*]]: !quake.qref) {
// CHECK:         %[[VAL_2:.*]] = arith.constant 1.000000e+00 : f32
// CHECK-DAG:     quake.x {{\[}}%[[VAL_0]] : !quake.qvec<?>] (%[[VAL_1]])
// CHECK-DAG:     %[[VAL_3:.*]] = arith.negf %[[VAL_2]] : f32
// CHECK:         quake.rx {{\[}}%[[VAL_0]] : !quake.qvec<?>] |%[[VAL_3]] : f32|(%[[VAL_1]])
// CHECK:         quake.h {{\[}}%[[VAL_0]] : !quake.qvec<?>] (%[[VAL_1]])
// CHECK:         quake.t<adj> {{\[}}%[[VAL_0]] : !quake.qvec<?>] (%[[VAL_1]])
// CHECK:         return
// CHECK:       }

// Note: the .ctrl variant is created as a side-effect of the implementation, so
// testing it is not strictly required here.

// CHECK-LABEL:   func.func private @test.ctrl(
// CHECK-SAME:       %[[VAL_0:.*]]: !quake.qvec<?>, %[[VAL_1:.*]]: !quake.qref) {
// CHECK:           quake.t {{\[}}%[[VAL_0]] : !quake.qvec<?>] (%[[VAL_1]])
// CHECK:           quake.h {{\[}}%[[VAL_0]] : !quake.qvec<?>] (%[[VAL_1]])
// CHECK:           %[[VAL_2:.*]] = arith.constant 1.000000e+00 : f32
// CHECK:           quake.rx {{\[}}%[[VAL_0]] : !quake.qvec<?>] |%[[VAL_2]] : f32|(%[[VAL_1]])
// CHECK:           quake.x {{\[}}%[[VAL_0]] : !quake.qvec<?>] (%[[VAL_1]])
// CHECK:           return
// CHECK:         }

// CHECK-LABEL:   func.func @test(
// CHECK-SAME:                    %[[VAL_0:.*]]: !quake.qref) {
// CHECK:           quake.t (%[[VAL_0]])
// CHECK:           quake.h (%[[VAL_0]])
// CHECK:           %[[VAL_1:.*]] = arith.constant 1.000000e+00 : f32
// CHECK:           quake.rx |%[[VAL_1]] : f32|(%[[VAL_0]])
// CHECK:           quake.x (%[[VAL_0]])
// CHECK:           return
// CHECK:         }

// CHECK-LABEL: func.func @do_apply(
// CHECK-SAME:     %[[VAL_0:.*]]: !quake.qref, %[[VAL_1:.*]]: !quake.qref) {
// CHECK:         %[[VAL_2:.*]] = quake.concat %[[VAL_1]] : (!quake.qref) -> !quake.qvec<?>
// CHECK:         call @test.adj.ctrl(%[[VAL_2]], %[[VAL_0]]) : (!quake.qvec<?>, !quake.qref) -> ()
// CHECK:         return
// CHECK:       }

// CHECK-QIR-LABEL: @do_apply
