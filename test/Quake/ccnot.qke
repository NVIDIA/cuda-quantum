// ========================================================================== //
// Copyright (c) 2022 - 2023 NVIDIA Corporation & Affiliates.                 //
// All rights reserved.                                                       //
//                                                                            //
// This source code and the accompanying materials are made available under   //
// the terms of the Apache License 2.0 which accompanies this distribution.   //
// ========================================================================== //

// RUN: cudaq-opt %s --inline --canonicalize | FileCheck %s 

module {

    // CHECK-LABEL: func.func @apply_x(
    // CHECK-SAME: %[[arg0:.*]]: !quake.ref) {
    // CHECK:   quake.x %[[arg0]] :
    // CHECK:   return
    // CHECK: }
    
    // CHECK-LABEL: func.func @ccnot() {
    // CHECK:   %[[a0:.*]] = quake.alloca !quake.veq<3>
    // CHECK:   affine.for %[[arg0:.*]] = 0 to 3 {
    // CHECK:     %[[a2:.*]] = quake.extract_ref %[[a0]][%[[arg0]]] : (!quake.veq<3>, index) -> !quake.ref
    // CHECK:     quake.x %[[a2]] :
    // CHECK:   }
    // CHECK:   %[[a1:.*]] = quake.extract_ref %[[a0]][1] : (!quake.veq<3>) -> !quake.ref
    // CHECK:   quake.x %[[a1]] :
    // CHECK:   return
    // CHECK: }
    
    func.func @apply_x(%q : !quake.ref) {
        quake.x %q : (!quake.ref) -> ()
        return
    }

    func.func @ccnot() {
        %c_3 = arith.constant 3 : i32
        %c_0 = arith.constant 0 : i32
        %c_1 = arith.constant 1 : i32
        %c_2 = arith.constant 2 : i32
        %qubits = quake.alloca !quake.veq<?> [ %c_3 : i32 ]
        %c_3_idx = arith.index_cast %c_3 : i32 to index
        affine.for %i = 0 to %c_3_idx {
            %q0 = quake.extract_ref %qubits [%i] : (!quake.veq<?>, index) -> !quake.ref
            quake.x %q0 : (!quake.ref) -> ()
        }

        %q1 = quake.extract_ref %qubits [%c_1] : (!quake.veq<?>, i32) -> !quake.ref
        func.call @apply_x(%q1) : (!quake.ref) -> ()

        return
    }
}
