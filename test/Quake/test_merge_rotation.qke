// ========================================================================== //
// Copyright (c) 2022 - 2023 NVIDIA Corporation & Affiliates.                 //
// All rights reserved.                                                       //
//                                                                            //
// This source code and the accompanying materials are made available under   //
// the terms of the Apache License 2.0 which accompanies this distribution.   //
// ========================================================================== //

// RUN: cudaq-opt --canonicalize %s | FileCheck %s

func.func @duplicate_rotation_check() {
  %0 = arith.constant 2 : i32
  %c_0 = arith.constant 0 : i32
  %qubits = quake.alloca !quake.veq<?>[ %0 : i32 ]
  %q0 = quake.extract_ref %qubits[%c_0] : (!quake.veq<?>,i32) -> !quake.ref
  %c_angle = arith.constant 0.59 : f64
  quake.rx (%c_angle) %q0: (f64, !quake.ref) -> ()
  quake.rx (%c_angle) %q0 : (f64, !quake.ref) -> ()
  %measurements0 = quake.mz %q0 : (!quake.ref) -> i1
  return
}

// CHECK-LABEL:   func.func @duplicate_rotation_check() {
// CHECK-DAG:       %[[VAL_0:.*]] = arith.constant 5.900000e-01 : f64
// CHECK-DAG:       %[[VAL_1:.*]] = quake.alloca !quake.veq<2>
// CHECK:           %[[VAL_2:.*]] = quake.extract_ref %[[VAL_1]][0] : (!quake.veq<2>) -> !quake.ref
// CHECK:           quake.rx (%[[VAL_0]]) %[[VAL_2]] : (f64, !quake.ref) -> ()
// CHECK:           quake.rx (%[[VAL_0]]) %[[VAL_2]] : (f64, !quake.ref) -> ()
// CHECK:           %[[VAL_3:.*]] = quake.mz %[[VAL_2]] : (!quake.ref) -> i1
// CHECK:           return
// CHECK:         }

func.func @duplicate_rotation_check2() {
  %0 = arith.constant 2 : i32
  %c_0 = arith.constant 0 : i32
  %qubits = quake.alloca  !quake.veq<?>[ %0 : i32]
  %q0 = quake.extract_ref %qubits[%c_0] : (!quake.veq<?>, i32) -> !quake.ref
  %c_angle = arith.constant 0.59 : f64
  %c_angle2 = arith.constant 0.23 : f64
  quake.rx (%c_angle) %q0 : (f64, !quake.ref) -> ()
  quake.rx (%c_angle) %q0 : (f64, !quake.ref) -> ()
  quake.rx (%c_angle2) %q0 : (f64, !quake.ref) -> ()
  %measurement = quake.mz %q0 : (!quake.ref) -> i1
  return
}

// CHECK-LABEL:   func.func @duplicate_rotation_check2() {
// CHECK-DAG:       %[[VAL_0:.*]] = arith.constant 2.300000e-01 : f64
// CHECK-DAG:       %[[VAL_1:.*]] = arith.constant 5.900000e-01 : f64
// CHECK-DAG:       %[[VAL_2:.*]] = quake.alloca !quake.veq<2>
// CHECK:           %[[VAL_3:.*]] = quake.extract_ref %[[VAL_2]][0] : (!quake.veq<2>) -> !quake.ref
// CHECK:           quake.rx (%[[VAL_1]]) %[[VAL_3]] : (f64, !quake.ref) -> ()
// CHECK:           quake.rx (%[[VAL_1]]) %[[VAL_3]] : (f64, !quake.ref) -> ()
// CHECK:           quake.rx (%[[VAL_0]]) %[[VAL_3]] : (f64, !quake.ref) -> ()
// CHECK:           %[[VAL_4:.*]] = quake.mz %[[VAL_3]] : (!quake.ref) -> i1
// CHECK:           return
// CHECK:         }

func.func @returns_angle(%arg0 : f64, %arg1 : f64) -> (f64) {
  %0 = arith.divf %arg0, %arg1 : f64
  return %0 : f64
}

// CHECK-LABEL:   func.func @returns_angle(
// CHECK-SAME:      %[[VAL_0:.*]]: f64, %[[VAL_1:.*]]: f64) -> f64 {
// CHECK:           %[[VAL_2:.*]] = arith.divf %[[VAL_0]], %[[VAL_1]] : f64
// CHECK:           return %[[VAL_2]] : f64
// CHECK:         }

func.func @duplicate_rotation_check3() {
  %0 = arith.constant 2 : i32
  %c_0 = arith.constant 0 : i32
  %qubits = quake.alloca  !quake.veq<?> [ %0 : i32 ]
  %q0 = quake.extract_ref %qubits[%c_0] : (!quake.veq<?>, i32) -> !quake.ref
  %c_angle = arith.constant 0.59 : f64
  %c_angle2 = arith.constant 0.23 : f64
  %c_angle3 = arith.constant 0.64 : f64
  %new_angle = call @returns_angle(%c_angle, %c_angle2) : (f64, f64) -> (f64) 
  %new_angle2 = call @returns_angle(%c_angle3, %c_angle2) : (f64, f64) -> (f64)
  quake.rx (%c_angle) %q0 : (f64, !quake.ref) -> ()
  quake.rx (%new_angle2) %q0 : (f64, !quake.ref) -> ()
  quake.rx (%new_angle) %q0 : (f64, !quake.ref) -> ()
  %measurement = quake.mz %q0 : (!quake.ref) -> i1
  return
}

// CHECK-LABEL:   func.func @duplicate_rotation_check3() {
// CHECK-DAG:       %[[VAL_0:.*]] = arith.constant 6.400000e-01 : f64
// CHECK-DAG:       %[[VAL_1:.*]] = arith.constant 2.300000e-01 : f64
// CHECK-DAG:       %[[VAL_2:.*]] = arith.constant 5.900000e-01 : f64
// CHECK-DAG:       %[[VAL_3:.*]] = quake.alloca !quake.veq<2>
// CHECK:           %[[VAL_4:.*]] = quake.extract_ref %[[VAL_3]][0] : (!quake.veq<2>) -> !quake.ref
// CHECK:           %[[VAL_5:.*]] = call @returns_angle(%[[VAL_2]], %[[VAL_1]]) : (f64, f64) -> f64
// CHECK:           %[[VAL_6:.*]] = call @returns_angle(%[[VAL_0]], %[[VAL_1]]) : (f64, f64) -> f64
// CHECK:           quake.rx (%[[VAL_2]]) %[[VAL_4]] : (f64, !quake.ref) -> ()
// CHECK:           quake.rx (%[[VAL_6]]) %[[VAL_4]] : (f64, !quake.ref) -> ()
// CHECK:           quake.rx (%[[VAL_5]]) %[[VAL_4]] : (f64, !quake.ref) -> ()
// CHECK:           %[[VAL_7:.*]] = quake.mz %[[VAL_4]] : (!quake.ref) -> i1
// CHECK:           return
// CHECK:         }

