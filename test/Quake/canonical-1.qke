// ========================================================================== //
// Copyright (c) 2022 - 2023 NVIDIA Corporation & Affiliates.                 //
// All rights reserved.                                                       //
//                                                                            //
// This source code and the accompanying materials are made available under   //
// the terms of the Apache License 2.0 which accompanies this distribution.   //
// ========================================================================== //

// RUN: cudaq-opt --canonicalize --cse %s | FileCheck %s

func.func @test1(%arg0 : !quake.veq<?>) -> i1 {
  %true = arith.constant true
  return %true : i1
}

func.func @test2() {
  %0 = arith.constant 10 : i64
  %1 = quake.alloca !quake.veq<?>[%0 : i64]
  // relax_size must be inserted here
  %2 = call @test1(%1) : (!quake.veq<?>) -> i1
  %3 = arith.constant 1 : i64
  %4 = quake.extract_ref %1[%3] : (!quake.veq<?>,i64) -> !quake.ref
  quake.h %4 : (!quake.ref) -> ()
  return
}

// CHECK-LABEL:   func.func @test2() {
// CHECK:           %[[VAL_1:.*]] = quake.alloca !quake.veq<10>
// CHECK:           %[[VAL_2:.*]] = quake.relax_size %[[VAL_1]] : (!quake.veq<10>) -> !quake.veq<?>
// CHECK:           %[[VAL_3:.*]] = call @test1(%[[VAL_2]]) : (!quake.veq<?>) -> i1
// CHECK:           %[[VAL_4:.*]] = quake.extract_ref %[[VAL_1]][1] : (!quake.veq<10>) -> !quake.ref
// CHECK:           quake.h %[[VAL_4]]
// CHECK:           return
// CHECK:         }

func.func @test3() {
  %0 = arith.constant 10 : i64
  %1 = quake.alloca !quake.veq<?>[%0 : i64]
  %2 = arith.constant 4 : i64
  %3 = arith.constant 7 : i64
  // This subvec veq<?> can be reified to veq<4>
  %4 = quake.subveq %1, %2, %3 : (!quake.veq<?>, i64, i64) -> !quake.veq<?>
  %5 = arith.constant 2 : i64
  %6 = quake.extract_ref %4[%5] : (!quake.veq<?>,i64) -> !quake.ref
  quake.h %6 : (!quake.ref) -> ()
  return
}

// CHECK-LABEL:   func.func @test3() {
// CHECK:           %[[VAL_1:.*]] = arith.constant 7 : i64
// CHECK:           %[[VAL_2:.*]] = arith.constant 4 : i64
// CHECK:           %[[VAL_3:.*]] = quake.alloca !quake.veq<10>
// CHECK:           %[[VAL_4:.*]] = quake.subveq %[[VAL_3]], %[[VAL_2]], %[[VAL_1]] : (!quake.veq<10>, i64, i64) -> !quake.veq<4>
// CHECK:           %[[VAL_5:.*]] = quake.extract_ref %[[VAL_4]][2] : (!quake.veq<4>) -> !quake.ref
// CHECK:           quake.h %[[VAL_5]] : (!quake.ref) -> ()
// CHECK:           return
// CHECK:         }

func.func @test_qextract_1() {
    %c0 = arith.constant 0 : i64
    %c2 = arith.constant 2 : i64
    %veq = quake.alloca !quake.veq<?>[%c2 : i64]
    %0 = quake.extract_ref %veq[%c0] : (!quake.veq<?>, i64) -> !quake.ref
    quake.h %0 : (!quake.ref) -> ()
    %c0_0 = arith.constant 0 : i64
    %c1 = arith.constant 1 : i64
    %1 = quake.extract_ref %veq[%c0_0] : (!quake.veq<?>, i64) -> !quake.ref
    %2 = quake.extract_ref %veq[%c1] : (!quake.veq<?>, i64) -> !quake.ref
    quake.x [%1] %2 : (!quake.ref, !quake.ref) -> ()
    return
}

// CHECK-LABEL:   func.func @test_qextract_1() {
// CHECK:           %[[VAL_2:.*]] = quake.alloca !quake.veq<2>
// CHECK:           %[[VAL_3:.*]] = quake.extract_ref %[[VAL_2]][0] : (!quake.veq<2>) -> !quake.ref
// CHECK:           quake.h %[[VAL_3]] : (!quake.ref) -> ()
// CHECK:           %[[VAL_4:.*]] = quake.extract_ref %[[VAL_2]][1] : (!quake.veq<2>) -> !quake.ref
// CHECK:           quake.x [%[[VAL_3]]] %[[VAL_4]] : (!quake.ref, !quake.ref) -> ()
// CHECK:           return
// CHECK:         }

func.func @test_qextract_2(%arg0: i1) {
    %c0 = arith.constant 0 : i64
    %c2 = arith.constant 2 : i64
    %c1 = arith.constant 1 : i64
    %veq = quake.alloca !quake.veq<?>[%c2 : i64]
    %0 = quake.extract_ref %veq[%c0] : (!quake.veq<?>,i64) -> !quake.ref
    quake.h %0 : (!quake.ref) -> ()
    cc.if(%arg0) {
      cc.scope {
        %1 = quake.extract_ref %veq[%c0] : (!quake.veq<?>,i64) -> !quake.ref
        %2 = quake.extract_ref %veq[%c1] : (!quake.veq<?>,i64) -> !quake.ref
        quake.x [%1] %2 : (!quake.ref,!quake.ref) -> ()
      }
    }
    %3 = quake.extract_ref %veq[%c0] : (!quake.veq<?>,i64) -> !quake.ref
    %4 = quake.extract_ref %veq[%c1] : (!quake.veq<?>,i64) -> !quake.ref
    quake.x [%3] %4 : (!quake.ref,!quake.ref) -> ()
    return
}

// CHECK-LABEL:   func.func @test_qextract_2(
// CHECK-SAME:                               %[[VAL_0:.*]]: i1) {
// CHECK:           %[[VAL_3:.*]] = quake.alloca !quake.veq<2>
// CHECK:           %[[VAL_4:.*]] = quake.extract_ref %[[VAL_3]][0] : (!quake.veq<2>) -> !quake.ref
// CHECK:           quake.h %[[VAL_4]] :
// CHECK:           cc.if(%[[VAL_0]]) {
// CHECK:             %[[VAL_6:.*]] = quake.extract_ref %[[VAL_3]][1] : (!quake.veq<2>) -> !quake.ref
// CHECK:             quake.x [%[[VAL_4]]] %[[VAL_6]] :
// CHECK:           }
// CHECK:           %[[VAL_7:.*]] = quake.extract_ref %[[VAL_3]][1] : (!quake.veq<2>) -> !quake.ref
// CHECK:           quake.x [%[[VAL_4]]] %[[VAL_7]] :
// CHECK:           return
// CHECK:         }
