// ========================================================================== //
// Copyright (c) 2022 - 2025 NVIDIA Corporation & Affiliates.                 //
// All rights reserved.                                                       //
//                                                                            //
// This source code and the accompanying materials are made available under   //
// the terms of the Apache License 2.0 which accompanies this distribution.   //
// ========================================================================== //

// RUN: cudaq-opt %s -split-input-file -verify-diagnostics

func.func @test_struq() {
  %0 = quake.alloca !quake.veq<4>
  %1 = arith.constant 1 : i32
  %2 = arith.constant 2.0 : f32
  // expected-error@+2 {{invalid struq member type}}
  // expected-error@+1 {{must be non-struct quantum reference type}}
  %6 = quake.make_struq %0, %1, %2 : (!quake.veq<4>, i32, f32) -> !quake.struq<!quake.veq<?>, i32, f32>
  return
}

// -----

func.func @test_struq() {
  %0 = quake.alloca !quake.veq<4>
  %1 = quake.alloca !quake.veq<7>
  // expected-error@+1 {{member type not compatible with operand type}}
  %6 = quake.make_struq %0, %1 : (!quake.veq<4>, !quake.veq<7>) -> !quake.struq<!quake.veq<3>, !quake.veq<8>>
  return
}

// -----

func.func @test_struq() {
  %0 = quake.alloca !quake.veq<4>
  %1 = quake.alloca !quake.veq<7>
  // expected-error@+1 {{result type has different member count than operands}}
  %6 = quake.make_struq %0, %1 : (!quake.veq<4>, !quake.veq<7>) -> !quake.struq<!quake.veq<?>>
  return
}

// -----

func.func @test_struq() {
  %0 = quake.alloca !quake.veq<4>
  %1 = quake.alloca !quake.veq<7>
  // expected-error@+1 {{result type has different member count than operands}}
  %6 = quake.make_struq %0, %1 : (!quake.veq<4>, !quake.veq<7>) -> !quake.struq<!quake.veq<?>, !quake.veq<?>, !quake.veq<?>>
  return
}

// -----

func.func @test_struq(%arg : !quake.struq<!quake.veq<1>, !quake.veq<2>, !quake.veq<3>>) {
  // expected-error@+1 {{invalid index}}
  %6 = quake.get_member %arg[3] : (!quake.struq<!quake.veq<1>, !quake.veq<2>, !quake.veq<3>>) -> !quake.veq<1>
  return
}

// -----

func.func @test_struq() {
  // expected-error@+1 {{struq type must have specified size}}
  %0 = quake.alloca !quake.struq<!quake.veq<1>, !quake.veq<?>>
  return
}

// -----

func.func @test_struq() {
  // expected-error@+1 {{invalid struq member type}}
  %0 = quake.alloca !quake.struq<!quake.struq<!quake.veq<3>, !quake.ref>, !quake.veq<7>>
  return
}

// -----

func.func @test_subveq(%0: !quake.veq<2>) {
  // expected-error@+1 {{does not fully intersect}}
  %1 = quake.subveq %0, 10, 10 : (!quake.veq<2>) -> !quake.veq<1>
  return
}

// -----

func.func @test_subveq(%0: !quake.veq<?>) {
  // expected-error@+1 {{invalid subrange specified}}
  %1 = quake.subveq %0, 10, 4 : (!quake.veq<?>) -> !quake.veq<?>
  return
}

// -----

func.func @test_subveq(%0: !quake.veq<?>) {
  // expected-error@+1 {{incorrect size for result}}
  %1 = quake.subveq %0, 8, 35 : (!quake.veq<?>) -> !quake.veq<1>
  return
}

// -----

func.func @test_insert_value(%0: !cc.struct<{i32, i8}>) {
  %2 = arith.constant 4 : i32
  // expected-error@+1 {{result type does not match input}}
  %1 = cc.insert_value %0[0], %2 : (!cc.struct<{i32, i8}>, i32) -> !cc.struct<{i32, i16}>
  return
}

// -----

func.func @test_insert_value(%0: !cc.array<i32 x ?>) {
  %2 = arith.constant 4 : i32
  // expected-error@+1 {{array must have constant size}}
  %1 = cc.insert_value %0[0], %2 : (!cc.array<i32 x ?>, i32) -> !cc.array<i32 x ?>
  return
}

// -----

func.func @test_insert_value(%0: !cc.array<i32 x 8>) {
  %2 = arith.constant 4 : i32
  // expected-error@+1 {{array cannot index out of bounds elements}}
  %1 = cc.insert_value %0[12], %2 : (!cc.array<i32 x 8>, i32) -> !cc.array<i32 x 8>
  return
}

// -----

func.func @test_insert_value(%0: !cc.struct<{i8, i16, i32, i64}>) {
  %2 = arith.constant 4 : i32
  // expected-error@+1 {{struct cannot index out of bounds members}}
  %1 = cc.insert_value %0[12], %2 : (!cc.struct<{i8, i16, i32, i64}>, i32) -> !cc.struct<{i8, i16, i32, i64}>
  return
}

// -----

func.func @test_insert_value(%0: complex<f64>) {
  %2 = arith.constant 4.0 : f64
  // expected-error@+1 {{complex index is out of bounds}}
  %1 = cc.insert_value %0[2], %2 : (complex<f64>, f64) -> complex<f64>
  return
}

// -----

func.func @test_insert_value(%0: !cc.array<!cc.struct<{i8, complex<f64>}> x 6>) {
  %2 = arith.constant 4.0 : f64
  // expected-error@+1 {{too many indices}}
  %1 = cc.insert_value %0[2, 1, 0, 0], %2 : (!cc.array<!cc.struct<{i8, complex<f64>}> x 6>, f64) -> !cc.array<!cc.struct<{i8, complex<f64>}> x 6>
  return
}

// -----

func.func @test_insert_value(%0: !cc.array<!cc.struct<{i8, complex<f64>}> x 6>) {
  %2 = arith.constant 4 : i64
  // expected-error@+1 {{value type does not match}}
  %1 = cc.insert_value %0[2, 1, 0], %2 : (!cc.array<!cc.struct<{i8, complex<f64>}> x 6>, i64) -> !cc.array<!cc.struct<{i8, complex<f64>}> x 6>
  return
}

// -----

func.func @test_cast_truncation_1(%0: i64) -> i32 {
  // expected-error@+1 {{to integer extension, not truncation}}
  %1 = cc.cast unsigned %0 : (i64) -> i32
  return %1 : i32
}

// -----

func.func @test_cast_truncation_2(%0: i64) -> i32 {
  // expected-error@+1 {{to integer extension, not truncation}}
  %1 = cc.cast signed %0 : (i64) -> i32
  return %1 : i32
}
